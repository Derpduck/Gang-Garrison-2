<?xml version="1.0" encoding="UTF-8" standalone="no"?>
<event category="STEP" id="0">
  <actions>
    <action id="603" library="1">
      <!--action name: Code-->
      <kind>CODE</kind>
      <allowRelative>false</allowRelative>
      <question>false</question>
      <canApplyTo>true</canApplyTo>
      <actionType>CODE</actionType>
      <functionName/>
      <relative>false</relative>
      <not>false</not>
      <appliesTo>.self</appliesTo>
      <arguments>
        <argument kind="STRING">if (instance_exists(NotificationMessage)) exit;

if(!variable_local_exists("baseybegin"))
{
    baseybegin = ybegin;
    mouse_samples = ds_list_create();
}
stepHasRun = true;

var itemcount, tabcount;
menu_hack_backbutton();
itemcount = menu_get_itemcount();
tabcount = menu_get_tabcount();

// Menu Scrolling
var menuSize;
menuSize = baseybegin + ((itemcount - 1) * spacing) + 16;
if (drawbg)
        menuSize += bgheight;

if (menuSize &gt; view_hview[0])
{
    ds_list_add(mouse_samples, window_mouse_get_y());
    while(ds_list_size(mouse_samples) &gt; 10)
        ds_list_delete(mouse_samples, 0);
    var des_y;
    des_y = 0;
    for(i = 0; i &lt; ds_list_size(mouse_samples); i += 1)
        des_y += ds_list_find_value(mouse_samples, i);
    des_y /= ds_list_size(mouse_samples);
    
    // Offset menu from start position by this amount
    ybegin = round(baseybegin
                   - min(1, max(0, des_y/view_hview[0]))
                     *(menuSize-view_hview[0]));
}

if(editing != -1) {
    // Handle input when editing
    switch (item_type[editing]) {
        case "edittext2":
        case "edittext":
        case "editnum":
        case "editfloat":
            if (keyboard_check(vk_control) &amp;&amp; keyboard_check(ord("V"))) {
                keyboard_string += clipboard_get_text();
                keyboard_clear(ord("V"));
            }
            item_value[editing] = keyboard_string;
            break;
        case "editkeyormouse":
            if mouse_check_button(mb_left) {
                menu_set_var(editing, MOUSE_LEFT);
                execute_string(item_script[editing], MOUSE_LEFT);
                editing = -1;
                exit;
            }
            else if mouse_check_button(mb_right) {
                menu_set_var(editing, MOUSE_RIGHT);
                execute_string(item_script[editing], MOUSE_RIGHT);
                editing = -1;
                exit;
            }
        case "editkey":
            if keyboard_check_direct(vk_tab) keyboard_lastkey = vk_tab;
            if keyboard_lastkey != 0 {
                menu_set_var(editing, keyboard_lastkey);
                execute_string(item_script[editing], menu_get_var(editing));
                editing = -1;
                exit;
            }
    }
} else if(mouse_x &gt; view_xview[0]+xbegin - tabmargin - tabwidth) {
    // Handle selecting menu items
    if(mouse_y &gt;= view_yview[0]+ybegin-(spacing/2) and mouse_x &lt; view_xview[0]+xbegin+width and mouse_x &gt; view_xview[0]+xbegin)
    {
        // Items
        virtualitem = max(0, round((mouse_y-(view_yview[0]+ybegin))/spacing));
        if(mouse_check_button_pressed(mb_left)){
            mouse_clear(mb_left);
            if(virtualitem &lt; itemcount) {
                switch (item_type[virtualitem]) {
                    case "script":
                        execute_string(item_script[virtualitem]);
                        break;
                    case "editscript":
                        item_value[virtualitem] = execute_string(item_script[virtualitem]);
                        break;
                    case "editkey":
                    case "editkeyormouse":
                        io_clear();
                        editing = virtualitem;
                        break;
                    case "edittext":
                    case "edittext2":
                    case "editnum":
                    case "editfloat":
                        keyboard_string = "";
                        editing = virtualitem;
                        break;
                    case "editselect":
                        item_value[virtualitem] = (item_value[virtualitem]+1) mod item_options[virtualitem];
                        menu_set_var(virtualitem, item_option_value[virtualitem, item_value[virtualitem]]);
                        execute_string(item_script[virtualitem], menu_get_var(virtualitem));
                        break;
                }
            }
        }
    }
    else if (mouse_y &gt;= view_yview[0] + baseybegin + taboffset - (spacing / 2) and mouse_x &lt; view_xview[0] + xbegin)
    {
        // Tabs
        virtualitem = min(-2, -2-round((mouse_y-(view_yview[0]+baseybegin+taboffset))/spacing));
        if(mouse_check_button_pressed(mb_left))
        {
            mouse_clear(mb_left);
            if(-2-virtualitem &lt; tabcount)
            {
                execute_string(tab_script[-2-virtualitem]);
            }
        }
    } else {
        virtualitem = -1;
    }
} else {
    virtualitem = -1;
}
</argument>
      </arguments>
    </action>
  </actions>
</event>
